BACKUP ~faiths_and_powers/backup~
AUTHOR ~SubtleD and Grammarsalad~


VERSION ~0.75.24~


//README ~faiths_and_powers/readme-pnp.html~


ALWAYS

	ACTION_IF NOT FILE_EXISTS ~override/d5_fnp_settings.ini~ BEGIN
  	  COPY ~faiths_and_powers/d5_fnp_settings.ini~ ~override~
	END
  	
  	OUTER_SPRINT ~mod_folder~ ~faiths_and_powers~

  	INCLUDE ~override/d5_fnp_settings.ini~

	INCLUDE ~faiths_and_powers/lib/remove_lines.tpa~

	INCLUDE ~faiths_and_powers/lib/spell_to_innate.tpa~

	INCLUDE ~faiths_and_powers/lib/fl#add_kit_ee.tpa~

	INCLUDE ~faiths_and_powers/lib/spell_clone_effects.tpa~
	
	INCLUDE ~faiths_and_powers/lib/joinable_npcs.tpa~

	INCLUDE ~faiths_and_powers/lib/fnp_compat.tpa~
	
	INCLUDE ~faiths_and_powers/lib/qd_multiclass.tpa~

	ACTION_IF FILE_EXISTS_IN_GAME ~a#ooze.2da~ BEGIN
	  COPY_EXISTING ~alignmnt.2da~ ~override~
		REPLACE_TEXTUALLY ~DRUID.+$~ ~DRUID	0	1	0	1	1	1	0	1	0 ~
		REPLACE_TEXTUALLY ~FIGHTER_DRUID.+$~ ~FIGHTER_DRUID	0	1	0	1	1	1	0	1	0 ~
		REPLACE_TEXTUALLY ~TOTEMIC_DRUID.+$~ ~TOTEMIC_DRUID	0	1	0	1	1	1	0	1	0 ~
		REPLACE_TEXTUALLY ~SHAPESHIFTER.+$~ ~SHAPESHIFTER	0	1	0	1	1	1	0	1	0 ~
		REPLACE_TEXTUALLY ~BEAST_FRIEND.+$~ ~BEAST_FRIEND	0	1	0	1	1	1	0	1	0 ~
	END

// joinable NPC issues for tutu/bgt/eet:
  ACTION_IF GAME_IS ~bgee~ BEGIN
    OUTER_SPRINT tutu_var ~~
    OUTER_SPRINT imoen6_var ~imoen6~
    OUTER_SPRINT jaheir7_var ~jaheir7~
    OUTER_SPRINT minsc7_var ~minsc7~
    OUTER_SPRINT quayle_var ~quayle~
    OUTER_SPRINT viconi6_var ~viconi6~
    OUTER_SPRINT xan_var ~xan~
    OUTER_SPRINT xzar_var ~xzar~
  END ELSE ACTION_IF GAME_IS ~eet~ BEGIN
    OUTER_SPRINT tutu_var ~~
    OUTER_SPRINT imoen6_var ~imoen6_~
    OUTER_SPRINT jaheir7_var ~jaheir7_~
    OUTER_SPRINT minsc7_var ~minsc7_~
    OUTER_SPRINT quayle_var ~quayle_~
    OUTER_SPRINT viconi6_var ~viconi6_~
    OUTER_SPRINT xan_var ~xan_~
    OUTER_SPRINT xzar_var ~xzar_~
  END ELSE ACTION_IF GAME_IS ~bgt~ BEGIN
    OUTER_SPRINT tutu_var ~~
    OUTER_SPRINT imoen6_var ~imoen61~
    OUTER_SPRINT quayle_var ~bgquayle~
    OUTER_SPRINT viconi6_var ~viconi61~
    OUTER_SPRINT xan_var ~bgxan~
    OUTER_SPRINT xzar_var ~bgxzar~
  END ELSE ACTION_IF GAME_IS ~tutu tutu_totsc~ BEGIN
    OUTER_SPRINT tutu_var ~_~
    OUTER_SPRINT imoen6_var ~imoen6~
    OUTER_SPRINT quayle_var ~quayle~
    OUTER_SPRINT viconi6_var ~viconi6~
    OUTER_SPRINT xan_var ~xan~
    OUTER_SPRINT xzar_var ~xzar~
  END

END

// ASK_EVERY_COMPONENT

LANGUAGE
  ~English~
  ~english~
  ~faiths_and_powers/language/english/setup.tra~
//___________________________________________________________________________________




//__________________________________________________________________________________
//__________________________________________________________________________________
//
//					CHOOSE WHETHER/HOW TO INSTALL NEW KITS
//__________________________________________________________________________________
//__________________________________________________________________________________

//__________________________________________________________________________________
//__________________________________________________________________________________
//
//					EVERYTHING, USING DEITY SELECTION DIALOGUE
//__________________________________________________________________________________
//__________________________________________________________________________________

BEGIN @101
DESIGNATED 101
DEPRECATED @18


//__________________________________________________________________________________
//__________________________________________________________________________________
//
//					102: ALL KITS + SPHERE SYSTEM; KITS IN MENUS
//__________________________________________________________________________________
//__________________________________________________________________________________

BEGIN @102 // all the new kits, and kits in menus
DESIGNATED 102
SUBCOMPONENT @100 /*(GAME_IS ~bgee bg2ee eet iwdee~)*/

//determine which sphere system to use______________________________________________
//
LAM d5_sphere_system

ACTION_IF (%d5_use_spheres% = 1) BEGIN
  LAM d5_2E_spheres
  ACTION_IF (FILE_EXISTS_IN_GAME ~fnp_sphere_list.tpa~) BEGIN
	  INCLUDE ~override/fnp_sphere_list.tpa~
  END
  ACTION_IF !(FILE_EXISTS_IN_GAME ~fnp_sphere_list.tpa~) BEGIN
	ACTION_IF (%d5_2E_spheres% = 1) BEGIN
	  COPY ~faiths_and_powers/lib/sphere_list_2E.tpa~ ~override/fnp_sphere_list.tpa~
	  INCLUDE ~override/fnp_sphere_list.tpa~
	END
	ACTION_IF (%d5_2E_spheres% = 0) BEGIN
	  ACTION_IF NOT FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~ BEGIN
		INCLUDE ~faiths_and_powers/lib/sphere_list_base.tpa~
	  END
	  ACTION_IF FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~ THEN BEGIN
		ACTION_IF GAME_IS ~iwdee~ BEGIN
		  INCLUDE ~faiths_and_powers/lib/sphere_list_iwd_sr.tpa~
		END
		ELSE BEGIN
		  INCLUDE ~faiths_and_powers/lib/sphere_list_sr.tpa~
		END
	  END
	END
  END
END	
//__________________________________________________________________________________

//Acolyte Lore (added to clabs)_____________________________________________________
//
COPY ~faiths_and_powers/kits/acolytes/B_LOR.spl~ ~override~
//__________________________________________________________________________________

//copy marker files_________________________________________________________________
//
COPY ~faiths_and_powers/lib/markers/d5_menu.d5~ ~override~
//__________________________________________________________________________________

//read in kit functions_____________________________________________________________
//
INCLUDE ~faiths_and_powers/lib/add_cleric_kits.tpa~
INCLUDE ~faiths_and_powers/lib/add_acolyte_kits.tpa~
INCLUDE ~faiths_and_powers/lib/add_incarnate_kits.tpa~
INCLUDE ~faiths_and_powers/lib/add_druid_kits.tpa~
INCLUDE ~faiths_and_powers/lib/add_mystic_kits.tpa~
INCLUDE ~faiths_and_powers/lib/add_occultist_kits.tpa~
INCLUDE ~faiths_and_powers/lib/add_ranger_kits.tpa~
INCLUDE ~faiths_and_powers/lib/add_scout_kits.tpa~
INCLUDE ~faiths_and_powers/lib/add_champion_kits.tpa~
INCLUDE ~faiths_and_powers/lib/add_zealot_kits.tpa~
INCLUDE ~faiths_and_powers/lib/add_multiclass_kits.tpa~
//__________________________________________________________________________________

//multiclass text____________________________________________________________________
//
ACTION_IF (FILE_EXISTS_IN_GAME ~clastext.2da~) BEGIN
  	COPY_EXISTING ~clastext.2da~ ~override~
		COUNT_2DA_COLS cols
		READ_2DA_ENTRIES_NOW rows cols  
		FOR (row = 1; row < rows; ++row) BEGIN
		  READ_2DA_ENTRY_FORMER rows row 0 ~text~
		  PATCH_IF ~%text%~ STRING_EQUAL_CASE ~CLERIC~ BEGIN
		    SET c_row = %row%
		  END
		  PATCH_IF ~%text%~ STRING_EQUAL_CASE ~FIGHTER_CLERIC~ BEGIN
		    SET fc_row = %row%
		  END
		  PATCH_IF ~%text%~ STRING_EQUAL_CASE ~CLERIC_MAGE~ BEGIN
		    SET cm_row = %row%
		  END
		  PATCH_IF ~%text%~ STRING_EQUAL_CASE ~CLERIC_THIEF~ BEGIN
		    SET ct_row = %row%
		  END
		  PATCH_IF ~%text%~ STRING_EQUAL_CASE ~CLERIC_RANGER~ BEGIN
		    SET cr_row = %row%
		  END
		  PATCH_IF ~%text%~ STRING_EQUAL_CASE ~FIGHTER_DRUID~ BEGIN
		    SET fd_row = %row%
		  END
		END
		SET_2DA_ENTRY %c_row% 3 cols RESOLVE_STR_REF (@10001)
		SET_2DA_ENTRY %c_row% 5 cols RESOLVE_STR_REF (@10002)
		SET_2DA_ENTRY %c_row% 4 cols RESOLVE_STR_REF (@10005)
		SET_2DA_ENTRY %fc_row% 3 cols RESOLVE_STR_REF (@10011)
		SET_2DA_ENTRY %fc_row% 5 cols RESOLVE_STR_REF (@10012)
		SET_2DA_ENTRY %fc_row% 4 cols RESOLVE_STR_REF (@10015)
		SET_2DA_ENTRY %cm_row% 3 cols RESOLVE_STR_REF (@10021)
		SET_2DA_ENTRY %cm_row% 5 cols RESOLVE_STR_REF (@10022)
		SET_2DA_ENTRY %cm_row% 4 cols RESOLVE_STR_REF (@10025)
		SET_2DA_ENTRY %ct_row% 3 cols RESOLVE_STR_REF (@10031)
		SET_2DA_ENTRY %ct_row% 5 cols RESOLVE_STR_REF (@10032)
		SET_2DA_ENTRY %ct_row% 4 cols RESOLVE_STR_REF (@10035)
		SET_2DA_ENTRY %cr_row% 3 cols RESOLVE_STR_REF (@10041)
		SET_2DA_ENTRY %cr_row% 5 cols RESOLVE_STR_REF (@10042)
		SET_2DA_ENTRY %cr_row% 4 cols RESOLVE_STR_REF (@10045)
		SET_2DA_ENTRY %fd_row% 3 cols RESOLVE_STR_REF (@10051)
		SET_2DA_ENTRY %fd_row% 5 cols RESOLVE_STR_REF (@10052)
		SET_2DA_ENTRY %fd_row% 4 cols RESOLVE_STR_REF (@10055)
  	BUT_ONLY
END
ACTION_IF (FILE_EXISTS_IN_GAME ~sodcltxt.2da~) BEGIN
  	COPY_EXISTING ~sodcltxt.2da~ ~override~
		COUNT_2DA_COLS cols // amount of columns
		READ_2DA_ENTRIES_NOW rows cols // read all file into memory  
		FOR (row = 1; row < rows; ++row) BEGIN // iterate over rows
		  READ_2DA_ENTRY_FORMER rows row 0 ~text~ // read column value
		  PATCH_IF ~%text%~ STRING_EQUAL_CASE ~CLERIC~ BEGIN
		    SET c_row = %row%
		  END
		  PATCH_IF ~%text%~ STRING_EQUAL_CASE ~FIGHTER_CLERIC~ BEGIN
		    SET fc_row = %row%
		  END
		  PATCH_IF ~%text%~ STRING_EQUAL_CASE ~CLERIC_MAGE~ BEGIN
		    SET cm_row = %row%
		  END
		  PATCH_IF ~%text%~ STRING_EQUAL_CASE ~CLERIC_THIEF~ BEGIN
		    SET ct_row = %row%
		  END
		  PATCH_IF ~%text%~ STRING_EQUAL_CASE ~CLERIC_RANGER~ BEGIN
		    SET cr_row = %row%
		  END
		  PATCH_IF ~%text%~ STRING_EQUAL_CASE ~FIGHTER_DRUID~ BEGIN
		    SET fd_row = %row%
		  END
		END
		SET_2DA_ENTRY %c_row% 3 cols RESOLVE_STR_REF (@10001)
		SET_2DA_ENTRY %c_row% 5 cols RESOLVE_STR_REF (@10002)
		SET_2DA_ENTRY %c_row% 4 cols RESOLVE_STR_REF (@10005)
		SET_2DA_ENTRY %fc_row% 3 cols RESOLVE_STR_REF (@10011)
		SET_2DA_ENTRY %fc_row% 5 cols RESOLVE_STR_REF (@10012)
		SET_2DA_ENTRY %fc_row% 4 cols RESOLVE_STR_REF (@10015)
		SET_2DA_ENTRY %cm_row% 3 cols RESOLVE_STR_REF (@10021)
		SET_2DA_ENTRY %cm_row% 5 cols RESOLVE_STR_REF (@10022)
		SET_2DA_ENTRY %cm_row% 4 cols RESOLVE_STR_REF (@10025)
		SET_2DA_ENTRY %ct_row% 3 cols RESOLVE_STR_REF (@10031)
		SET_2DA_ENTRY %ct_row% 5 cols RESOLVE_STR_REF (@10032)
		SET_2DA_ENTRY %ct_row% 4 cols RESOLVE_STR_REF (@10035)
		SET_2DA_ENTRY %cr_row% 3 cols RESOLVE_STR_REF (@10041)
		SET_2DA_ENTRY %cr_row% 5 cols RESOLVE_STR_REF (@10042)
		SET_2DA_ENTRY %cr_row% 4 cols RESOLVE_STR_REF (@10045)
		SET_2DA_ENTRY %fd_row% 3 cols RESOLVE_STR_REF (@10051)
		SET_2DA_ENTRY %fd_row% 5 cols RESOLVE_STR_REF (@10052)
		SET_2DA_ENTRY %fd_row% 4 cols RESOLVE_STR_REF (@10055)
  	BUT_ONLY
END
//__________________________________________________________________________________

//enable lots of multiclasses_______________________________________________________
//
ACTION_IF FILE_EXISTS_IN_GAME ~CLSRCREQ.2da~ BEGIN
 COPY_EXISTING ~clsrcreq.2da~ ~override~
  COUNT_2DA_ROWS 8 rows
  FOR (row = 0 ; row < rows ; ++row) BEGIN
    READ_2DA_ENTRY row 0 8 entry
    PATCH_IF (("%entry%" STRING_COMPARE_CASE "FIGHTER_CLERIC"      = 0) OR
              ("%entry%" STRING_COMPARE_CASE "CLERIC_THIEF"        = 0)) BEGIN // cleric/thief, fighter/cleric
      SET_2DA_ENTRY  row 1 8 1
      SET_2DA_ENTRY  row 2 8 1
      SET_2DA_ENTRY  row 3 8 1
      SET_2DA_ENTRY  row 4 8 1
      SET_2DA_ENTRY  row 5 8 1
      SET_2DA_ENTRY  row 6 8 1
      SET_2DA_ENTRY  row 7 8 1
    END
    PATCH_IF ("%entry%" STRING_COMPARE_CASE "CLERIC_MAGE"         = 0) BEGIN // cleric/mage
      SET_2DA_ENTRY  row 1 8 1
      SET_2DA_ENTRY  row 2 8 1
      SET_2DA_ENTRY  row 3 8 1
      SET_2DA_ENTRY  row 6 8 1
    END
    PATCH_IF ("%entry%" STRING_COMPARE_CASE "CLERIC_RANGER"       = 0) BEGIN // cleric/ranger
      SET_2DA_ENTRY  row 1 8 1
      SET_2DA_ENTRY  row 2 8 1
      SET_2DA_ENTRY  row 3 8 1
    END
    PATCH_IF (("%entry%" STRING_COMPARE_CASE "FIGHTER_MAGE"        = 0) OR
              ("%entry%" STRING_COMPARE_CASE "FIGHTER_THIEF"       = 0) OR
              ("%entry%" STRING_COMPARE_CASE "FIGHTER_MAGE_THIEF"  = 0) OR
              ("%entry%" STRING_COMPARE_CASE "MAGE_THIEF"          = 0)) BEGIN // other human multiclasses
      SET_2DA_ENTRY  row 1 8 1
    END
    PATCH_IF (("%entry%" STRING_COMPARE_CASE "DRUID"               = 0) OR
    		  ("%entry%" STRING_COMPARE_CASE "FIGHTER_DRUID"       = 0)) BEGIN // druids and mystics
      SET_2DA_ENTRY  row 1 8 1
      SET_2DA_ENTRY  row 2 8 1
      SET_2DA_ENTRY  row 3 8 1
      SET_2DA_ENTRY  row 4 8 1
      SET_2DA_ENTRY  row 5 8 1
      SET_2DA_ENTRY  row 6 8 1
      SET_2DA_ENTRY  row 7 8 1
    END
    PATCH_IF ("%entry%" STRING_COMPARE_CASE "FIGHTER_MAGE_CLERIC" = 0) BEGIN // fighter/mage/cleric
      SET_2DA_ENTRY  row 1 8 0
      SET_2DA_ENTRY  row 2 8 0
      SET_2DA_ENTRY  row 3 8 0
      SET_2DA_ENTRY  row 4 8 0
      SET_2DA_ENTRY  row 5 8 0
      SET_2DA_ENTRY  row 6 8 0
      SET_2DA_ENTRY  row 7 8 0
    END
  END
  PRETTY_PRINT_2DA
 BUT_ONLY
END
//___________________________________________________________________________________

//change druid class text____________________________________________________________
//
ACTION_IF (FILE_EXISTS_IN_GAME ~clastext.2da~) BEGIN
  COPY_EXISTING ~clastext.2da~ ~override~
	COUNT_2DA_COLS cols // amount of columns
	READ_2DA_ENTRIES_NOW rows cols // read all file into memory  
	FOR (row = 1; row < rows; ++row) BEGIN // iterate over rows
	  READ_2DA_ENTRY_FORMER rows row 0 ~text~ // read column value
	  PATCH_IF ~%text%~ STRING_EQUAL_CASE ~DRUID~ BEGIN
	    SET patch_row = %row%
	  END
	END
	SET_2DA_ENTRY %patch_row% 3 cols RESOLVE_STR_REF (@70001)
	SET_2DA_ENTRY %patch_row% 5 cols RESOLVE_STR_REF (@70002)
	SET_2DA_ENTRY %patch_row% 4 cols RESOLVE_STR_REF (@70003)
  BUT_ONLY
END
ACTION_IF (FILE_EXISTS_IN_GAME ~sodcltxt.2da~) BEGIN
  COPY_EXISTING ~sodcltxt.2da~ ~override~
	COUNT_2DA_COLS cols // amount of columns
	READ_2DA_ENTRIES_NOW rows cols // read all file into memory  
	FOR (row = 1; row < rows; ++row) BEGIN // iterate over rows
	  READ_2DA_ENTRY_FORMER rows row 0 ~text~ // read column value
	  PATCH_IF ~%text%~ STRING_EQUAL_CASE ~DRUID~ BEGIN
	    SET patch_row = %row%
	  END
	END
	SET_2DA_ENTRY %patch_row% 3 cols RESOLVE_STR_REF (@70001)
	SET_2DA_ENTRY %patch_row% 5 cols RESOLVE_STR_REF (@70002)
	SET_2DA_ENTRY %patch_row% 4 cols RESOLVE_STR_REF (@70003)
  BUT_ONLY
END
//__________________________________________________________________________________

//druids on cleric spell/xp tables__________________________________________________
// from BG2Tweaks

COPY_EXISTING ~xplevel.2da~ ~override~
REPLACE_TEXTUALLY
~CLERIC \(.*\)
DRUID \(.*\)~
~CLERIC \1
DRUID  \1~
BUT_ONLY

ACTION_IF FILE_EXISTS_IN_GAME ~lunumab.2da~ THEN BEGIN
  COPY_EXISTING ~lunumab.2da~ ~override~
    READ_2DA_ENTRY  2 1 5 "cleric"   // read trueclass cleric value for initial HLA level
    SET_2DA_ENTRY   6 1 5 "%cleric%" // write trueclass cleric value for initial HLA level into druid slot
    READ_2DA_ENTRY  9 1 5 "f_c"      // read dual f/c value for initial HLA level
    SET_2DA_ENTRY  15 1 5 "%f_c%"    // write dual f/c cleric value for initial HLA level into dual f/d slot
    READ_2DA_ENTRY 22 1 5 "multic"   // read multiclass cleric value for initial HLA level
    SET_2DA_ENTRY  24 1 5 "%multic%" // write multiclass cleric value for initial HLA level into multiclass druid slot
  BUT_ONLY
END

COPY_EXISTING ~mxsplprs.2da~ ~override/mxspldru.2da~
//__________________________________________________________________________________

//ranger/paladin spell tables_______________________________________________________
//
COPY ~faiths_and_powers/spheres/mxsplpal.2da~ ~override~
COPY ~faiths_and_powers/spheres/mxsplran.2da~ ~override~
//__________________________________________________________________________________

//change shaman known spell tables__________________________________________________
//
COPY ~faiths_and_powers/kits/incarnates/splshmkn.2da~ ~override~
//__________________________________________________________________________________

//Add IWD spells if not already in game_____________________________________________
//
ACTION_IF GAME_IS ~bgee bg2ee eet~ BEGIN
 ACTION_IF NOT MOD_IS_INSTALLED ~setup-iwdification.tp2~ ~40~ BEGIN  //If IWDification not installed...
  ACTION_IF NOT FILE_EXISTS_IN_GAME ~b_IWD.itm~ BEGIN    			//If b_ IWD spells not installed...
	COPY ~faiths_and_powers/compatibility/b_IWD.itm~ ~override~   	//to detect this component 
	INCLUDE ~faiths_and_powers/lib/Setup_IWD_divine_Spells.tpa~    	//install them
  END
 END
END
//__________________________________________________________________________________

//Create Sphere System______________________________________________________________
//
ACTION_IF (%d5_use_spheres% = 1) BEGIN
 INCLUDE ~faiths_and_powers/lib/create_sphere_system.tpa~
 LAF create_spheres END
END
//__________________________________________________________________________________

//install the kits__________________________________________________________________
//

LAM d5_kit_choices

ACTION_IF (%d5_use_spheres% = 0) BEGIN
   OUTER_SET d5_multiclass_druids =	0
   OUTER_SET d5_earth_mystic 	=	0
   OUTER_SET d5_water_mystic 	=	0
   OUTER_SET d5_air_mystic 		=	0
   OUTER_SET d5_fire_mystic 	=	0
   OUTER_SET d5_light_mystic 	=	0
   OUTER_SET d5_shadow_mystic 	=	0
   OUTER_SET d5_elementalist 	=	0
END

// (clerics)
ACTION_IF (%d5_cleric_lathander% = 1) BEGIN
 LAF cleric_lathander END
END
ACTION_IF (%d5_cleric_torm% = 1) BEGIN
 LAF cleric_torm END
END
ACTION_IF (%d5_cleric_tymora% = 1) BEGIN
 LAF cleric_tymora END
END
ACTION_IF (%d5_cleric_helm% = 1) BEGIN
 LAF cleric_helm END
END
ACTION_IF (%d5_cleric_kelemvor% = 1) BEGIN
 ACTION_IF NOT GAME_IS ~iwdee~ BEGIN
  LAF cleric_kelemvor END
 END
END
ACTION_IF (%d5_cleric_tempus% = 1) BEGIN
 LAF cleric_tempus END
END
ACTION_IF (%d5_cleric_leira% = 1) BEGIN
 LAF cleric_leira END
END
ACTION_IF (%d5_cleric_baravar% = 1) BEGIN
 LAF cleric_baravar END
END
ACTION_IF (%d5_cleric_loviatar% = 1) BEGIN
 LAF cleric_loviatar END
END
ACTION_IF (%d5_cleric_talos% = 1) BEGIN
 LAF cleric_talos END
END
ACTION_IF (%d5_cleric_cyric% = 1) BEGIN
 ACTION_IF NOT GAME_IS ~iwdee~ BEGIN
  LAF cleric_cyric END
 END
END
ACTION_IF (%d5_cleric_umberlee% = 1) BEGIN
 LAF cleric_umberlee END
END
//
// (acolytes)
ACTION_IF (%d5_acolyte_ilmater% = 1) BEGIN
 LAF acolyte_ilmater END
END
ACTION_IF (%d5_acolyte_deneir% = 1) BEGIN
 LAF acolyte_deneir END
END
ACTION_IF (%d5_acolyte_sune% = 1) BEGIN
 LAF acolyte_sune END
END
ACTION_IF (%d5_acolyte_tempus% = 1) BEGIN
 LAF acolyte_tempus END
END
ACTION_IF (%d5_acolyte_mask% = 1) BEGIN
 LAF acolyte_mask END
END
ACTION_IF (%d5_acolyte_shar% = 1) BEGIN
 LAF acolyte_shar END
END
ACTION_IF (%d5_acolyte_moander% = 1) BEGIN
 LAF acolyte_moander END
END
ACTION_IF (%d5_acolyte_beshaba% = 1) BEGIN
 LAF acolyte_beshaba END
END
ACTION_IF (%d5_acolyte_baervan% = 1) BEGIN
 LAF acolyte_baervan END
END
//
// (incarnates)
// LAF incarnate_mystra
//
// (druids)
ACTION_IF (%d5_forest_druid% = 1) BEGIN
 LAF forest_druid END
END
ACTION_IF (%d5_totemic_druid% = 1) BEGIN
 LAF totemic_druid END
END
ACTION_IF (%d5_hivekeeper_druid% = 1) BEGIN
 LAF hivekeeper_druid END
END
ACTION_IF (%d5_northern_druid% = 1) BEGIN
 LAF northern_druid END
END
ACTION_IF (%d5_lost_druid% = 1) BEGIN
 LAF lost_druid END
END
ACTION_IF (%d5_multiclass_druids% = 1) BEGIN
 LAF fighter_druid END
 LAF druid_mage END
 LAF druid_thief END
 LAF druid_ranger END
END
ACTION_IF (%d5_forest_druid% + %d5_totemic_druid% + %d5_hivekeeper_druid% + %d5_northern_druid% + %d5_lost_druid% + %d5_multiclass_druids% > 0) BEGIN
 LAF druid_shapeshifting END
END
//
// (mystics)
ACTION_IF (%d5_earth_mystic% = 1) BEGIN
 LAF earth_mystic END
END
ACTION_IF (%d5_water_mystic% = 1) BEGIN
 LAF water_mystic END
END
ACTION_IF (%d5_air_mystic% = 1) BEGIN
 LAF air_mystic END
END
ACTION_IF (%d5_fire_mystic% = 1) BEGIN
 LAF fire_mystic END
END
ACTION_IF (%d5_light_mystic% = 1) BEGIN
 LAF light_mystic END
END
ACTION_IF (%d5_shadow_mystic% = 1) BEGIN
 LAF shadow_mystic END
END
//
// (occultists)
ACTION_IF (%d5_beast_lord% = 1) BEGIN
 LAF beast_lord END
END
ACTION_IF (%d5_elementalist% = 1) BEGIN
 LAF elementalist END
END
//
// (paladins)
ACTION_IF (%d5_champion_tyr% + %d5_champion_azuth% + %d5_champion_moradin% + %d5_champion_kelemvor% + %d5_champion_tempus% + %d5_champion_bane% + %d5_champion_helm% + %d5_champion_talos% + %d5_champion_red% > 0) BEGIN
 //any race can have their pallys
 COPY_EXISTING ~clsrcreq.2da~ ~override~ //Testing...
 //pally
    SET_2DA_ENTRY 29 2 8 1
    SET_2DA_ENTRY 29 3 8 1
    SET_2DA_ENTRY 29 4 8 1
    SET_2DA_ENTRY 29 5 8 1
    SET_2DA_ENTRY 29 6 8 1
    SET_2DA_ENTRY 29 7 8 1
 //b guard
    SET_2DA_ENTRY 33 2 8 1
    SET_2DA_ENTRY 33 3 8 1
    SET_2DA_ENTRY 33 4 8 1
    SET_2DA_ENTRY 33 5 8 1
    SET_2DA_ENTRY 33 6 8 1
    SET_2DA_ENTRY 33 7 8 1
 BUT_ONLY_IF_IT_CHANGES
 //Dwarves need to be able to take the champion class.  Cha 13 requirement
 COPY_EXISTING ~ABCLASRQ.2da~ ~override~ //
 //Pally
    SET_2DA_ENTRY 6 6 6 13
    SET_2DA_ENTRY 30 6 6 13
    SET_2DA_ENTRY 31 6 6 13
    SET_2DA_ENTRY 32 6 6 13
 BUT_ONLY_IF_IT_CHANGES
END

ACTION_IF (%d5_champion_tyr% = 1) BEGIN
 LAF champion_tyr END
END
ACTION_IF (%d5_champion_azuth% = 1) BEGIN
 LAF champion_azuth	 END	//azuth essentially replaces Inquisitor
END
ACTION_IF (%d5_champion_moradin% = 1) BEGIN
 LAF champion_moradin END
END
ACTION_IF (%d5_champion_kelemvor% = 1) BEGIN
 //champ of Kele effectively replaces UHunter. In IWDEE, the champ of mykrul is merely added (they're different enough conceptually to be seperate)
 ACTION_IF GAME_IS ~iwdee~ THEN BEGIN   //
  LAF iwdee_champion_kelemvor END
 END
 //kelevmore undead hunter for non-iwdee
 ACTION_IF GAME_IS ~bgee bg2ee eet~ THEN BEGIN   //
  LAF champion_kelemvor END
 END
END
ACTION_IF (%d5_champion_tempus% = 1) BEGIN
 ACTION_IF FILE_EXISTS_IN_GAME ~fallen.2da~ BEGIN  //No feasible way to make them not fall in IWDEE as yet
  LAF champion_tempus END
 END
END
ACTION_IF (%d5_champion_bane% = 1) BEGIN
 ACTION_IF FILE_EXISTS_IN_GAME ~fallen.2da~ BEGIN  //No Evil nonfallable pallys in IWDEE as yet
  LAF champion_bane END
 END
 ACTION_IF NOT FILE_EXISTS_IN_GAME ~fallen.2da~ BEGIN  //No Evil nonfallable pallys in IWDEE as yet
  LAF iwdee_champion_bane END
 END
END
ACTION_IF (%d5_champion_helm% = 1) BEGIN
 ACTION_IF FILE_EXISTS_IN_GAME ~fallen.2da~ BEGIN  //No Evil nonfallable pallys in IWDEE as yet
  LAF champion_helm END
 END
 ACTION_IF NOT FILE_EXISTS_IN_GAME ~fallen.2da~ BEGIN  //No Evil fallable pallys in IWDEE
  LAF iwdee_champion_helm END
 END
END
ACTION_IF (%d5_champion_talos% = 1) BEGIN
 ACTION_IF FILE_EXISTS_IN_GAME ~fallen.2da~ BEGIN  //No Evil nonfallable pallys in IWDEE
  LAF champion_talos END
 END
 ACTION_IF NOT FILE_EXISTS_IN_GAME ~fallen.2da~ BEGIN  //No Evil nonfallable pallys in IWDEE
  LAF iwdee_champion_talos END
 END
END
ACTION_IF (%d5_champion_red% = 1) BEGIN
 ACTION_IF FILE_EXISTS_IN_GAME ~fallen.2da~ BEGIN  //No feasible way to make them not fall in IWDEE without fallen.2da
  LAF champion_red END
 END
END
//
// (other paladins)
//
// (zealot)
ACTION_IF (%d5_zealot_ilmater% + %d5_zealot_corellon% + %d5_zealot_sune% + %d5_zealot_mystra% + %d5_zealot_eilistraee% + %d5_zealot_gruumsh% + %d5_zealot_talos% + %d5_zealot_kossuth% + %d5_zealot_garagos% > 0) BEGIN
 //sneaky sneaky
 COPY ~faiths_and_powers/kits/zealots/b_sneak.spl~ ~override~
 COPY ~faiths_and_powers/kits/zealots/b_snk02.spl~ ~override~
 COPY ~faiths_and_powers/kits/zealots/b_snk03.spl~ ~override~
 COPY ~faiths_and_powers/kits/zealots/b_snk04.spl~ ~override~
 COPY ~faiths_and_powers/kits/zealots/b_snk05.spl~ ~override~
 COPY ~faiths_and_powers/kits/zealots/b_snk06.spl~ ~override~
 COPY ~faiths_and_powers/kits/zealots/b_snk07.spl~ ~override~
 COPY ~faiths_and_powers/kits/zealots/b_snk08.spl~ ~override~
 COPY ~faiths_and_powers/kits/zealots/b_snk09.spl~ ~override~
 COPY ~faiths_and_powers/kits/zealots/b_snk10.spl~ ~override~
 COPY ~faiths_and_powers/kits/zealots/b_snk11.spl~ ~override~
 COPY ~faiths_and_powers/kits/zealots/b_snk12.spl~ ~override~
 COPY ~faiths_and_powers/kits/zealots/b_snk13.spl~ ~override~
END

ACTION_IF (%d5_zealot_ilmater% = 1) BEGIN
 LAF zealot_ilmater END
END
ACTION_IF (%d5_zealot_corellon% = 1) BEGIN
 LAF zealot_corellon END
END
ACTION_IF (%d5_zealot_sune% = 1) BEGIN
 LAF zealot_sune END
END
ACTION_IF (%d5_zealot_mystra% = 1) BEGIN
 LAF zealot_mystra END
END
ACTION_IF (%d5_zealot_eilistraee% = 1) BEGIN
 LAF zealot_eilistraee END
END
ACTION_IF FILE_EXISTS_IN_GAME ~fallen.2da~ BEGIN  //No feasible way to make them not fall in IWDEE w/o fallen.2da
 ACTION_IF (%d5_zealot_gruumsh% = 1) BEGIN
  LAF zealot_gruumsh END
 END
 ACTION_IF (%d5_zealot_talos% = 1) BEGIN
  LAF zealot_talos END
 END
 ACTION_IF (%d5_zealot_kossuth% = 1) BEGIN
  LAF zealot_kossuth END
 END
 ACTION_IF (%d5_zealot_garagos% = 1) BEGIN
  LAF zealot_garagos END
 END
END  //End grummsh/talos/kossuth/garagos
//
// (rangers)
ACTION_IF (%d5_forest_ranger% = 1) BEGIN
 LAF forest_ranger END
END
ACTION_IF (%d5_mountain_ranger% = 1) BEGIN
 LAF mountain_ranger END
END
ACTION_IF (%d5_jungle_ranger% = 1) BEGIN
 LAF jungle_ranger END
END
ACTION_IF (%d5_desert_ranger% = 1) BEGIN
 LAF desert_ranger END
END
ACTION_IF (%d5_arctic_ranger% = 1) BEGIN
 LAF arctic_ranger END
END
ACTION_IF (%d5_beast_master% = 1) BEGIN
 LAF beast_master END
END
//
// (scouts)
ACTION_IF (%d5_mage_hunter% = 1) BEGIN
 LAF mage_hunter END
END
ACTION_IF (%d5_barbarian_ranger% = 1) BEGIN
 LAF barbarian_ranger END
END
ACTION_IF (%d5_elven_archer% = 1) BEGIN
 LAF elven_archer END
END
ACTION_IF (%d5_halfling_slinger% = 1) BEGIN
 LAF halfling_slinger END
END
//
// (multiclass)
ACTION_IF (%d5_cm_mystra% = 1) BEGIN
 LAF cm_mystra END
END
ACTION_IF (%d5_cm_azuth% = 1) BEGIN
 LAF cm_azuth END
END
ACTION_IF (%d5_cm_bender% = 1) BEGIN
 LAF cm_bender END
END
ACTION_IF (%d5_cf_clangeddin% = 1) BEGIN
 LAF cf_clangeddin END
END
ACTION_IF (%d5_cr_mielikki% = 1) BEGIN
 LAF cr_mielikki END
END
ACTION_IF (%d5_cr_shaundakul% = 1) BEGIN
 LAF cr_shaundakul END
END
ACTION_IF (%d5_cf_helm% = 1) BEGIN
 LAF cf_helm END
END
/*
ACTION_IF (%d5_multi_sorcerers% = 1) BEGIN
 LAF multi_sorcerers END
END
*/
//
//__________________________________________________________________________________

//vanilla kits______________________________________________________________________
//
INCLUDE ~faiths_and_powers/lib/vanilla_kit_spheres.tpa~
LAF vanilla_kit_spheres END
//__________________________________________________________________________________

//mod kits__________________________________________________________________________
//
INCLUDE ~faiths_and_powers/lib/mod_kit_spheres.tpa~
LAF mod_kit_spheres END
//__________________________________________________________________________________

//default access____________________________________________________________________
//
INCLUDE ~faiths_and_powers/lib/default_kit_spheres.tpa~
LAF default_kit_spheres END
//__________________________________________________________________________________

//special kit: ur-priest
LAF cleric_ur_priest END
//__________________________________________________________________________________

//Base kits_________________________________________________________________________
//
INCLUDE ~faiths_and_powers/kits/acolytes/acolyte.tpa~
INCLUDE ~faiths_and_powers/kits/champions/champion.tpa~
INCLUDE ~faiths_and_powers/kits/zealots/zealot.tpa~
//__________________________________________________________________________________

//remove unwanted kits from menus___________________________________________________
//
COPY_EXISTING ~kitlist.2da~ ~override~
//	COUNT_2DA_COLS k_cols
	READ_2DA_ENTRIES_NOW k_rows 9
	FOR (k_row = 1; k_row < k_rows; ++k_row) BEGIN
	  READ_2DA_ENTRY_FORMER k_rows k_row 1 ~k_kit~
	  PATCH_IF ~%k_kit%~ STRING_EQUAL_CASE ~B_ACOLYTE~ BEGIN
	    READ_2DA_ENTRY_FORMER k_rows k_row 0 acolyte_num
	  END
	  PATCH_IF ~%k_kit%~ STRING_EQUAL_CASE ~B_ZEALOT_1~ BEGIN
	    READ_2DA_ENTRY_FORMER k_rows k_row 0 zealot_num
	  END
	END
BUT_ONLY

ACTION_FOR_EACH class IN ~C~ ~D~ ~P~ ~R~ ~FC~ ~FD~ ~CM~ ~CT~ ~CR~ BEGIN
  ACTION_FOR_EACH race IN ~D~ ~E~ ~G~ ~H~ ~HE~ ~HL~ ~HO~ BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~K_%class%_%race%.2da~ BEGIN
	  COPY_EXISTING ~K_%class%_%race%.2da~ ~override~
		COUNT_2DA_COLS cols
		READ_2DA_ENTRIES_NOW rows cols
		SET rows_removed = 0
		FOR (row = 1; row < rows; ++row) BEGIN
		  READ_2DA_ENTRY_FORMER rows row 1 kit
		  PATCH_IF (%kit% = 0) OR 
		  			(%kit% = 17) OR 
		  			(%kit% = 19) OR 
		  			(%kit% = 20) OR 
		  			(%kit% = 21) OR 
		  			(%kit% = 4) OR 
		  			(%kit% = 5) OR 
		  			(%kit% = 6) OR 
		  			(%kit% = acolyte_num) OR 
		  			(%kit% = zealot_num) BEGIN
			REMOVE_2DA_ROW (row - rows_removed) cols
			SET rows_removed += 1
		  END
		END
	  BUT_ONLY
	END
  END
END
//__________________________________________________________________________________

//halfling rangers__________________________________________________________________
//
ACTION_IF FILE_EXISTS_IN_GAME ~d5_sling.2da~ BEGIN
  COPY_EXISTING ~kitlist.2da~ ~override~
//	COUNT_2DA_COLS cols
	READ_2DA_ENTRIES_NOW rows 9
	FOR (row = 1; row < rows; ++row) BEGIN
	  READ_2DA_ENTRY_FORMER rows row 1 ~sling~
	  PATCH_IF ~%sling%~ STRING_EQUAL_CASE ~D5SLING~ BEGIN
	    SET sling_row = (%row% + 1)
	  END
	END
  BUT_ONLY
  COPY ~faiths_and_powers/kits/K_X_Y/K_R_HL.2da~ ~override~
	REPLACE_TEXTUALLY ~[ %TAB%]0~ ~%TAB%%sling_row%~
END
//__________________________________________________________________________________

//more scout alignments_____________________________________________________________
//
ACTION_PHP_EACH d5_fnp_kit_class_access AS kit => class BEGIN
  ACTION_IF (~%class%~ STRING_EQUAL_CASE ~SCOUT~) BEGIN
	COPY_EXISTING ~kitlist.2da~ ~override~
//	  COUNT_2DA_COLS cols
	  READ_2DA_ENTRIES_NOW rows 9
	  FOR (row = 7; row < rows; ++row) BEGIN
		READ_2DA_ENTRY_FORMER rows row 5 ~clab~
		PATCH_IF ~%clab%~ STRING_EQUAL_CASE ~%kit%~ BEGIN
		  SET kit_index = %row%
		  READ_2DA_ENTRY_FORMER rows kit_index 1 kit_label
		END
	  END
	BUT_ONLY
	APPEND ~fallen.2da~ ~%kit_label%%TAB%0~
	COPY_EXISTING ~alignmnt.2da~ ~override~
	  COUNT_2DA_COLS num_cols
	  READ_2DA_ENTRIES_NOW ~r2en_align~ num_cols
	  FOR (row = 3; row < r2en_align; row += 1) BEGIN
		READ_2DA_ENTRY_FORMER ~r2en_align~ row 0 align_name
		PATCH_IF (~%align_name%~ STRING_EQUAL_CASE ~%kit_label%~) BEGIN
		  FOR (col = 1; col < num_cols; col += 1) BEGIN
			SET_2DA_ENTRY_LATER ~s2el_align~ row col 1 // more alignments
		  END
		END
	  END
	  SET_2DA_ENTRIES_NOW ~s2el_align~ num_cols
	BUT_ONLY
  END
END
//__________________________________________________________________________________

//read fnp_compat info______________________________________________________________
//
LAM ~READ_FNP_KIT_INFO~
//__________________________________________________________________________________

//deity selection___________________________________________________________________
//
INCLUDE ~faiths_and_powers/lib/deity_selection.tpa~
LAF deity_dialogue END
//__________________________________________________________________________________

//alter item usability______________________________________________________________
//
LAM d5_item_usability

ACTION_IF (%d5_item_use% = 1) BEGIN
  INCLUDE ~faiths_and_powers/lib/item_usability.tpa~
  LAF change_usability END
END
//__________________________________________________________________________________

//Apply Sphere System_______________________________________________________________
//
ACTION_IF (%d5_use_spheres% = 1) BEGIN
  INCLUDE ~faiths_and_powers/lib/apply_sphere_system.tpa~
  LAF apply_spheres END
END
//__________________________________________________________________________________

//Handle NPCs_______________________________________________________________________
//
ACTION_IF (%d5_use_spheres% = 1) BEGIN
  INCLUDE ~faiths_and_powers/lib/npc_kits.tpa~
  LAF npc_kits END
END
//__________________________________________________________________________________

//no dual-classing__________________________________________________________________
//
LAM d5_dual_classing

ACTION_IF (%d5_dual_class% = 0) BEGIN
  COPY_EXISTING ~dualclas.2da~ ~override~
	COUNT_2DA_COLS cols
	COUNT_2DA_ROWS cols rows
	FOR (row = 0; row < (rows - 1); ++row) BEGIN
	  SET_2DA_ENTRY row 2 cols 0
	  SET_2DA_ENTRY row 6 cols 0
	END
	PRETTY_PRINT_2DA
  BUT_ONLY
END
//__________________________________________________________________________________



//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						 105: NO KITS, JUST SPHERE SYSTEM
//__________________________________________________________________________________
//__________________________________________________________________________________


//__________________________________________________________________________________
BEGIN @105 // no deity selection or new kits
DESIGNATED 105
SUBCOMPONENT @100

//copy marker file__________________________________________________________________
//
COPY ~faiths_and_powers/lib/markers/d5_nada.d5~ ~override~
//__________________________________________________________________________________

//determine which sphere system to use______________________________________________
//
  LAM d5_2E_spheres
  ACTION_IF (FILE_EXISTS_IN_GAME ~fnp_sphere_list.tpa~) BEGIN
	  INCLUDE ~override/fnp_sphere_list.tpa~
  END
  ACTION_IF !(FILE_EXISTS_IN_GAME ~fnp_sphere_list.tpa~) BEGIN
	ACTION_IF (%d5_2E_spheres% = 1) BEGIN
	  COPY ~faiths_and_powers/lib/sphere_list_2E.tpa~ ~override/fnp_sphere_list.tpa~
	END
	ACTION_IF (%d5_2E_spheres% = 0) BEGIN
	  ACTION_IF NOT FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~ BEGIN
		INCLUDE ~faiths_and_powers/lib/sphere_list_base.tpa~
	  END
	  ACTION_IF FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~ THEN BEGIN
		ACTION_IF GAME_IS ~iwdee~ BEGIN
		  INCLUDE ~faiths_and_powers/lib/sphere_list_iwd_sr.tpa~
		END
		ELSE BEGIN
		  INCLUDE ~faiths_and_powers/lib/sphere_list_sr.tpa~
		END
	  END
	END
  END
//__________________________________________________________________________________

//run the sphere system______________________________________________________________
//
INCLUDE ~faiths_and_powers/lib/create_sphere_system.tpa~
LAF create_spheres END
//__________________________________________________________________________________

//vanilla kits_______________________________________________________________________
//
INCLUDE ~faiths_and_powers/lib/vanilla_kit_spheres.tpa~
LAF vanilla_kit_spheres END
//___________________________________________________________________________________

//mod kits___________________________________________________________________________
//
INCLUDE ~faiths_and_powers/lib/mod_kit_spheres.tpa~
LAF mod_kit_spheres END
//___________________________________________________________________________________

//default access_____________________________________________________________________
//
INCLUDE ~faiths_and_powers/lib/default_kit_spheres.tpa~
LAF default_kit_spheres END
//___________________________________________________________________________________

//read fnp_compat info______________________________________________________________
//
LAM ~READ_FNP_KIT_INFO~
//__________________________________________________________________________________

//deity selection___________________________________________________________________
//
INCLUDE ~faiths_and_powers/lib/deity_selection.tpa~
LAF deity_dialogue END
//__________________________________________________________________________________

//alter item usability______________________________________________________________
//
LAM d5_item_usability

ACTION_IF (%d5_item_use% = 1) BEGIN
  INCLUDE ~faiths_and_powers/lib/item_usability.tpa~
  LAF change_usability END
END
//__________________________________________________________________________________

//Add IWD spells if not already in game
ACTION_IF GAME_IS ~bgee bg2ee eet~ BEGIN
 ACTION_IF NOT MOD_IS_INSTALLED ~setup-iwdification.tp2~ ~40~ BEGIN  //If IWDification not installed...
  ACTION_IF NOT FILE_EXISTS_IN_GAME ~b_IWD.itm~ BEGIN    			//If b_ IWD spells not installed...
	COPY ~faiths_and_powers/compatibility/b_IWD.itm~ ~override~   	//to detect this component 
	INCLUDE ~faiths_and_powers/lib/Setup_IWD_divine_Spells.tpa~    	//install them
  END
 END
END

//run the sphere system______________________________________________________________
//
INCLUDE ~faiths_and_powers/lib/apply_sphere_system.tpa~
LAF apply_spheres END
//__________________________________________________________________________________

//Handle NPCs_______________________________________________________________________
//
INCLUDE ~faiths_and_powers/lib/npc_kits.tpa~
LAF npc_kits END
//__________________________________________________________________________________

//verbiage__________________________________________________________________________
//
PRINT ~neither the dialogue system nor any new kits will be installed~
PRINT ~the sphere system will be installed over the vanilla kits and/or previously installed mod kits~
//__________________________________________________________________________________

