
ACTION_DEFINE_ASSOCIATIVE_ARRAY d5_fnp_spheres BEGIN
  Life => 0
  Death => 0
  Benediction => 0
  Destruction => 0
  Protection => 0
  War => 0
  Exploration => 0
  Knowledge => 0
  Deception => 0
  Thought => 0
  Dread => 0
  Vigor => 0
  Affliction => 0
  Animal => 0
  Plant => 0
  Earth => 0
  Water => 0
  Air => 0
  Fire => 0
  Light => 0
  Shadow => 0
  Magic => 0
  Perdition => 0
END

ACTION_DEFINE_ASSOCIATIVE_ARRAY d5_fnp_usable BEGIN
	LEATHER_ARMOR => 0
	CHAIN_ARMOR => 0
	PLATE_ARMOR => 0
	CLUB_STAFF => 0
	HAMMERS => 0
	MACES => 0
	FLAILS => 0
	AXES => 0
	DAGGERS => 0
	SHORT_SWORDS => 0
	LONG_SWORDS => 0
	SCIMITARS => 0
	KATANAS => 0
	BASTARD_SWORDS => 0
	2HAND_SWORDS => 0
	HALBERDS => 0
	SPEARS => 0
	DARTS => 0
	SLINGS => 0
	BOWS => 0
	CROSSBOWS => 0  
END

ACTION_DEFINE_ASSOCIATIVE_ARRAY d5_fnp_sphere_access BEGIN
  X => 0
  MAJOR => 0
  MINOR => 0
  FOCUS => 0
  PALADIN => 0
  RANGER => 0
  INCARNATE => 0
END

ACTION_DEFINE_ASSOCIATIVE_ARRAY d5_fnp_class_access BEGIN
  X => 0
  CLERIC => 0
  ACOLYTE => 0
  FIGHTER_CLERIC => 0
  CLERIC_MAGE => 0
  CLERIC_THIEF => 0
  RANGER_CLERIC => 0
  DRUID => 0
  MYSTIC => 0
  OCCULTIST => 0
  FIGHTER_MYSTIC => 0
  RANGER => 0
  SCOUT => 0
  CHAMPION => 0
  ZEALOT => 0
  SHAMAN => 0
END

DEFINE_ACTION_FUNCTION ~DEFINE_FNP_KIT_INFO~
  INT_VAR
	u_leather_armor = 1
	u_chain_armor = 0
	u_plate_armor = 0
	u_club_staff = 1
	u_hammers = 0
	u_maces = 0
	u_flails = 0
	u_axes = 0
	u_daggers = 0
	u_short_swords = 0
	u_long_swords = 0
	u_scimitars = 0
	u_katanas = 0
	u_bastard_swords = 0
	u_2hand_swords = 0
	u_halberds = 0
	u_spears = 0
	u_darts = 0
	u_slings = 0
	u_bows = 0
	u_crossbows = 0  
  STR_VAR
    clab_name = ~d5defa~
    class = ~x~
    s_Life = ~x~
    s_Death = ~x~
    s_Benediction = ~x~
    s_Destruction = ~x~
    s_Protection = ~x~
    s_War = ~x~
	s_Exploration = ~x~
    s_Knowledge = ~x~
    s_Deception = ~x~
    s_Thought = ~x~
    s_Dread = ~x~
    s_Vigor = ~x~
    s_Affliction = ~x~
    s_Animal = ~x~
    s_Plant = ~x~
    s_Earth = ~x~
    s_Water = ~x~
    s_Air = ~x~
    s_Fire = ~x~
    s_Light = ~x~
    s_Shadow = ~x~
    s_Magic = ~x~
	s_Perdition = ~x~
    2da_folder = EVAL ~%MOD_FOLDER%/lib~
BEGIN
  PRINT ~Defining Faiths and Powers info for kit %clab_name% ...~ 
  
  // validate required files exist
  ACTION_IF (NOT FILE_EXISTS ~%2da_folder%/d5_class.2da~
               || NOT FILE_EXISTS ~%2da_folder%/d5_usabl.2da~
               || NOT FILE_EXISTS ~%2da_folder%/d5_spher.2da~) BEGIN
    FAIL ~DEFINE_FNP_KIT_INFO: The required d5_class.2da, d5_usabl.2da, and d5_spher.2da template files could not be found in the folder %2da_folder%.~
  END
  
  // validate class
  ACTION_TO_UPPER class
  ACTION_IF NOT (VARIABLE_IS_SET $d5_fnp_class_access(~%class%~)) BEGIN
    FAIL ~DEFINE_FNP_KIT_INFO: The specified class (%class%) does not match one of the valid options: ALL, HUMAN, ELF, DWARF, GNOME, HALFLING, or ORC.~
  END
  
  // validate spheres
  ACTION_PHP_EACH d5_fnp_spheres AS sphere => val BEGIN
//    ACTION_TO_LOWER sphere
    OUTER_TEXT_SPRINT sphere ~s_%sphere%~
    OUTER_TEXT_SPRINT access EVAL ~%%sphere%%~
    ACTION_TO_UPPER %access%
    ACTION_IF (NOT VARIABLE_IS_SET $d5_fnp_sphere_access(~%access%~)) BEGIN
      FAIL ~DEFINE_FNP_KIT_INFO: The access value (%access%) for the sphere %sphere% does not match one of the valid options: X, MAJOR, MINOR, FOCUS, PALADIN, or RANGER.~
    END
  END

  // warn if CLAB could not be found
  ACTION_IF (NOT FILE_EXISTS_IN_GAME ~%clab_name%.2da~) BEGIN
    WARN ~DEFINE_FNP_KIT_INFO: The CLAB file (%clab_name%.2da) could not be found in the game.~
  END
  
  // copy over template files if they aren't present yet
  ACTION_IF (NOT FILE_EXISTS_IN_GAME ~d5_class.2da~) BEGIN
    COPY ~%2da_folder%/d5_class.2da~ ~override~
  END
  ACTION_IF (NOT FILE_EXISTS_IN_GAME ~d5_usabl.2da~) BEGIN
    COPY ~%2da_folder%/d5_usabl.2da~ ~override~
  END
  ACTION_IF (NOT FILE_EXISTS_IN_GAME ~d5_spher.2da~) BEGIN
    COPY ~%2da_folder%/d5_spher.2da~ ~override~
  END

  ACTION_IF NOT (FILE_CONTAINS_EVALUATED (~d5_class.2da~ ~%clab_name%~)) BEGIN

	ACTION_IF FILE_EXISTS_IN_GAME ~fnp_sphere_list.tpa~ BEGIN
	  INCLUDE ~override/fnp_sphere_list.tpa~
	END
	ACTION_PHP_EACH EVAL ~%clab_name%_spheres~ AS this_variable => kit_access BEGIN
	  ACTION_IF FILE_EXISTS_IN_GAME ~d5altsph.2da~ BEGIN
		COPY_EXISTING ~d5altsph.2da~ ~override~
		  READ_2DA_ENTRIES_NOW rows 2
		  FOR (row = 0; row < rows; ++row) BEGIN
			READ_2DA_ENTRY_FORMER rows row 0 ~fnp_sphere~
			READ_2DA_ENTRY_FORMER rows row 1 ~alt_sphere~
			PATCH_IF ~%this_variable%~ STRING_EQUAL_CASE ~%alt_sphere%~ BEGIN
			  SPRINT new_variable ~%fnp_sphere%~
			END
		  END
		BUT_ONLY
	  END ELSE BEGIN
		OUTER_SPRINT new_variable ~%this_variable%~
	  END
	  ACTION_IF (~%new_variable%~ STRING_EQUAL_CASE ~Life~) BEGIN
		OUTER_SPRINT s_Life ~%kit_access%~
	  END
	  ACTION_IF (~%new_variable%~ STRING_EQUAL_CASE ~Death~) BEGIN
		OUTER_SPRINT s_Death ~%kit_access%~
	  END
	  ACTION_IF (~%new_variable%~ STRING_EQUAL_CASE ~Benediction~) BEGIN
		OUTER_SPRINT s_Benediction ~%kit_access%~
	  END
	  ACTION_IF (~%new_variable%~ STRING_EQUAL_CASE ~Destruction~) BEGIN
		OUTER_SPRINT s_Destruction ~%kit_access%~
	  END
	  ACTION_IF (~%new_variable%~ STRING_EQUAL_CASE ~Protection~) BEGIN
		OUTER_SPRINT s_Protection ~%kit_access%~
	  END
	  ACTION_IF (~%new_variable%~ STRING_EQUAL_CASE ~War~) BEGIN
		OUTER_SPRINT s_War ~%kit_access%~
	  END
	  ACTION_IF (~%new_variable%~ STRING_EQUAL_CASE ~Exploration~) BEGIN
		OUTER_SPRINT s_Exploration ~%kit_access%~
	  END
	  ACTION_IF (~%new_variable%~ STRING_EQUAL_CASE ~Knowledge~) BEGIN
		OUTER_SPRINT s_Knowledge ~%kit_access%~
	  END
	  ACTION_IF (~%new_variable%~ STRING_EQUAL_CASE ~Deception~) BEGIN
		OUTER_SPRINT s_Deception ~%kit_access%~
	  END
	  ACTION_IF (~%new_variable%~ STRING_EQUAL_CASE ~Thought~) BEGIN
		OUTER_SPRINT s_Thought ~%kit_access%~
	  END
	  ACTION_IF (~%new_variable%~ STRING_EQUAL_CASE ~Dread~) BEGIN
		OUTER_SPRINT s_Dread ~%kit_access%~
	  END
	  ACTION_IF (~%new_variable%~ STRING_EQUAL_CASE ~Vigor~) BEGIN
		OUTER_SPRINT s_Vigor ~%kit_access%~
	  END
	  ACTION_IF (~%new_variable%~ STRING_EQUAL_CASE ~Affliction~) BEGIN
		OUTER_SPRINT s_Affliction ~%kit_access%~
	  END
	  ACTION_IF (~%new_variable%~ STRING_EQUAL_CASE ~Animal~) BEGIN
		OUTER_SPRINT s_Animal ~%kit_access%~
	  END
	  ACTION_IF (~%new_variable%~ STRING_EQUAL_CASE ~Plant~) BEGIN
		OUTER_SPRINT s_Plant ~%kit_access%~
	  END
	  ACTION_IF (~%new_variable%~ STRING_EQUAL_CASE ~Earth~) BEGIN
		OUTER_SPRINT s_Earth ~%kit_access%~
	  END
	  ACTION_IF (~%new_variable%~ STRING_EQUAL_CASE ~Air~) BEGIN
		OUTER_SPRINT s_Air ~%kit_access%~
	  END
	  ACTION_IF (~%new_variable%~ STRING_EQUAL_CASE ~Water~) BEGIN
		OUTER_SPRINT s_Water ~%kit_access%~
	  END
	  ACTION_IF (~%new_variable%~ STRING_EQUAL_CASE ~Fire~) BEGIN
		OUTER_SPRINT s_Fire ~%kit_access%~
	  END
	  ACTION_IF (~%new_variable%~ STRING_EQUAL_CASE ~Light~) BEGIN
		OUTER_SPRINT s_Light ~%kit_access%~
	  END
	  ACTION_IF (~%new_variable%~ STRING_EQUAL_CASE ~Shadow~) BEGIN
		OUTER_SPRINT s_Shadow ~%kit_access%~
	  END
	  ACTION_IF (~%new_variable%~ STRING_EQUAL_CASE ~Magic~) BEGIN
		OUTER_SPRINT s_Magic ~%kit_access%~
	  END
	  ACTION_IF (~%new_variable%~ STRING_EQUAL_CASE ~Perdition~) BEGIN
		OUTER_SPRINT s_Perdition ~%kit_access%~
	  END
    END

  // define the text that will be added to the 2da files
   OUTER_TEXT_SPRINT class_text ~%clab_name% %class%~
   OUTER_TEXT_SPRINT usable_text ~%clab_name% 	%u_leather_armor% %u_chain_armor% %u_plate_armor% %u_club_staff% %u_hammers% %u_maces% %u_flails% %u_axes% %u_daggers% %u_short_swords% %u_long_swords% %u_scimitars% %u_katanas% %u_bastard_swords% %u_2hand_swords% %u_halberds% %u_spears% %u_darts% %u_slings% %u_bows% %u_crossbows%~
   OUTER_TEXT_SPRINT sphere_text ~%clab_name% %s_Life% %s_Death% %s_Benediction% %s_Destruction% %s_Protection% %s_War% %s_Exploration% %s_Knowledge% %s_Deception% %s_Thought% %s_Dread% %s_Vigor% %s_Affliction% %s_Animal% %s_Plant% %s_Earth% %s_Water% %s_Air% %s_Fire% %s_Light% %s_Shadow% %s_Magic% %s_Perdition%~

  // uppercase text
   ACTION_TO_UPPER class_text
   ACTION_TO_UPPER usable_text
   ACTION_TO_UPPER sphere_text
    
  // append to the .2da files
   APPEND ~d5_class.2da~ ~%class_text%~
   APPEND ~d5_usabl.2da~ ~%usable_text%~
   APPEND ~d5_spher.2da~ ~%sphere_text%~
  
  // clean up the .2da files
   COPY_EXISTING ~d5_usabl.2da~ ~override~
                ~d5_class.2da~ ~override~
                ~d5_spher.2da~ ~override~
	PRETTY_PRINT_2DA
  END
END

DEFINE_ACTION_MACRO ~READ_FNP_KIT_INFO~ BEGIN

//  CLEAR_ARRAYS

  ACTION_IF (FILE_EXISTS_IN_GAME ~d5_class.2da~
               && FILE_EXISTS_IN_GAME ~d5_usabl.2da~
               && FILE_EXISTS_IN_GAME ~d5_spher.2da~) BEGIN
    
    // read kits and class access
    COPY_EXISTING ~d5_class.2da~ ~override~
      READ_2DA_ENTRIES_NOW ~r2en_d5_class~ 2
      FOR (row = 1; row < r2en_d5_class; row += 1) BEGIN
        READ_2DA_ENTRY_FORMER ~r2en_d5_class~ row 0 kit
        READ_2DA_ENTRY_FORMER ~r2en_d5_class~ row 1 class
        TEXT_SPRINT $d5_fnp_kits(~%row%~) ~%kit%~
        TEXT_SPRINT $d5_fnp_kit_class_access(~%kit%~) ~%class%~
      END
    
    // read usability access
    COPY_EXISTING ~d5_usabl.2da~ ~override~
      COUNT_2DA_COLS num_cols
      READ_2DA_ENTRIES_NOW ~r2en_d5_usable~ (num_cols - 1)
      FOR (row = 1; row < r2en_d5_usable; row += 1) BEGIN
        READ_2DA_ENTRY_FORMER ~r2en_d5_usable~ row 0 kit
        FOR (col = 0; col < (num_cols - 1); col += 1) BEGIN
          READ_2DA_ENTRY_FORMER ~r2en_d5_usable~ 0 col usable
          READ_2DA_ENTRY_FORMER ~r2en_d5_usable~ row (col + 1) access
          TEXT_SPRINT $d5_fnp_kit_usable_access(~%kit%~ ~%usable%~) ~%access%~
        END
      END
    
    // read sphere access
    COPY_EXISTING ~d5_spher.2da~ ~override~
      COUNT_2DA_COLS num_cols
      READ_2DA_ENTRIES_NOW ~r2en_d5_sphere~ (num_cols - 1)
      FOR (row = 1; row < r2en_d5_sphere; row += 1) BEGIN
        READ_2DA_ENTRY_FORMER ~r2en_d5_sphere~ row 0 kit
        FOR (col = 0; col < (num_cols - 1); col += 1) BEGIN
          READ_2DA_ENTRY_FORMER ~r2en_d5_sphere~ 0 col sphere
          READ_2DA_ENTRY_FORMER ~r2en_d5_sphere~ row (col + 1) access
          TEXT_SPRINT $d5_fnp_kit_sphere_access(~%sphere%~ ~%kit%~) ~%access%~
        END
      END
  END
END

DEFINE_ACTION_FUNCTION ~DISPLAY_FNP_KIT_INFO~ BEGIN
  ACTION_PHP_EACH d5_fnp_kits AS ind => kit BEGIN
    PRINT ~KIT: %kit%~
    ACTION_IF (VARIABLE_IS_SET $d5_fnp_kit_class_access(~%kit%~)) BEGIN
      OUTER_TEXT_SPRINT class $d5_fnp_kit_class_access(~%kit%~)
      PRINT ~  CLASS: %class%~
      PRINT ~  USABILITY:~
      ACTION_PHP_EACH d5_fnp_usable AS usable => val BEGIN
        ACTION_IF (VARIABLE_IS_SET $d5_fnp_kit_usable_access(~%kit%~ ~%usable%~)) BEGIN
          OUTER_TEXT_SPRINT access $d5_fnp_kit_usable_access(~%kit%~ ~%usable%~)
          ACTION_IF (IS_AN_INT access && access == 1) BEGIN
            PRINT ~    %usable%: %access%~
          END
        END
      END
      PRINT ~  SPHERES:~
      ACTION_PHP_EACH d5_fnp_spheres AS sphere => val BEGIN
        ACTION_IF (VARIABLE_IS_SET $d5_fnp_kit_sphere_access(~%sphere%~ ~%kit%~)) BEGIN
          OUTER_TEXT_SPRINT access $d5_fnp_kit_sphere_access(~%sphere%~ ~%kit%~)
          ACTION_IF (VARIABLE_IS_SET $d5_fnp_sphere_access(~%access%~) && NOT ~%access%~ STRING_EQUAL_CASE ~X~) BEGIN
            PRINT ~    %sphere%: %access%~
          END
        END
      END
    END
  END
END

// NECESSARY FUNCTIONS________________________________________________________________
// Don't change this stuff
//
DEFINE_ACTION_FUNCTION ~USE_ALT_SPHERES~ BEGIN
//ACTION_IF FILE_EXISTS_IN_GAME ~fnp_sphere_list.tpa~ BEGIN
//  INCLUDE ~override/fnp_sphere_list.tpa~
  ACTION_PHP_EACH alt_spheres AS spell => alt_sphere BEGIN
	OUTER_INNER_PATCH_SAVE fnp_sphere_name ~%alt_sphere%~ BEGIN
	  PHP_EACH sphere_name_changes AS fnp_sphere => new_sphere BEGIN
		REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~%new_sphere%~ ~%fnp_sphere%~
	  END
	END
	OUTER_SPRINT $spellsphere(~%spell%~ ~%spell_1%~) ~%fnp_sphere_name%~
  END
END
//____________________________________________________________________________________
